buildscript {
  ext.versions = [
      'kotlin': '1.3.70',
      'autoService': '1.0-rc6',
  ]

  ext.deps = [
      'kotlin': [
          'stdlib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
              'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
              'js': "org.jetbrains.kotlin:kotlin-stdlib-js:${versions.kotlin}"
          ],
          'embeddableCompiler': "org.jetbrains.kotlin:kotlin-compiler-embeddable:${versions.kotlin}",
          'gradlePlugin' : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'gradlePluginApi': "org.jetbrains.kotlin:kotlin-gradle-plugin-api:${versions.kotlin}",
      ],
      'autoService': [
          'annotations': "com.google.auto.service:auto-service-annotations:${versions.autoService}",
          'compiler': "com.google.auto.service:auto-service:${versions.autoService}",
      ],
      'asmUtil': 'org.ow2.asm:asm-util:7.3.1',
      'kotlinCompileTesting': 'com.github.tschuchortdev:kotlin-compile-testing:1.2.5',
      'junit': 'junit:junit:4.13',
      'truth': 'com.google.truth:truth:1.0.1',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.1.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.9.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.0'
  }

  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.35.0'
    disabledRules = [
        'filename',
    ]
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        freeCompilerArgs = [
            "-progressive",
        ]
      }
    }
  }
}

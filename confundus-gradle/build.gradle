apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.gmazzo.buildconfig'

dependencies {
  compileOnly gradleApi()
  compileOnly libs.kotlin.gradlePlugin

  testImplementation libs.junit
  testImplementation libs.assertk
  testImplementation gradleTestKit()
}

gradlePlugin {
  plugins {
    confundus {
      id = "com.jakewharton.confundus"
      displayName = "unsafeCast for Kotlin/JVM"
      description = "A port of Kotlin/JS's unsafeCast for Kotlin/JVM"
      implementationClass = "com.jakewharton.confundus.gradle.ConfundusPlugin"
    }
  }
}

test {
  dependsOn(':confundus-api:publishAllPublicationsToTestMavenRepository')
  dependsOn(':confundus-compiler:publishAllPublicationsToTestMavenRepository')
  dependsOn(':confundus-gradle:publishAllPublicationsToTestMavenRepository')
}

buildConfig {
  useKotlinOutput {
    internalVisibility = true
    topLevelConstants = true
  }

  packageName('com.jakewharton.confundus.gradle')
  buildConfigField("String", "ConfundusVersion", "\"$version\"")
}
